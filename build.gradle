plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'dev.jsinco'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "iridiumdevelopment"
        url = "https://nexus.iridiumdevelopment.net/repository/maven-releases/"
    }
    maven {
        name = "playerpoints"
        url = "https://repo.rosewooddev.io/repository/public/"
    }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "update-checker"
        url = "https://repo.jeff-media.com/public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly "org.black_ixx:playerpoints:3.2.5"
    compileOnly "me.clip:placeholderapi:2.11.3"

    implementation "com.jeff_media:SpigotUpdateChecker:3.0.3"
    implementation "org.xerial:sqlite-jdbc:3.42.0.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.iridium:IridiumColorAPI:1.0.6"
    implementation "org.jetbrains:annotations:24.0.0"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
kotlin {
    jvmToolchain(17)
}
shadowJar {
    dependencies {
        include(dependency("org.jetbrains.kotlin:kotlin-stdlib"))
        include(dependency("com.iridium:IridiumColorAPI:1.0.6"))
        include(dependency("com.jeff_media:SpigotUpdateChecker:3.0.3"))
    }
}
build.dependsOn shadowJar